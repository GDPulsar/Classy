plugins {
    id 'com.github.johnrengelman.shadow'
    id "com.modrinth.minotaur" version "2.+"
    id 'java-library'
    id 'maven-publish'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

modrinth {
    token = findProperty("modrinth.token")
    projectId = "classy"
    versionNumber = project.version
    versionType = "alpha"
    versionName = "Classy NeoForge $mod_version"

    uploadFile = project(":neoforge").tasks.remapJar

    gameVersions = ["1.21.1"]
    loaders = ["neoforge"]

    dependencies {
        required.project "architectury-api"
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    modImplementation "dev.architectury:architectury-neoforge:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}

publishing {
    publications {
        mavenCommon(MavenPublication) {
            from components.java

            artifactId = "classy-neoforge"

            pom {
                name = "Classy! NeoForge"
                description = "NeoForge Architectury package for Classy!"
                url = "https://github.com/GDPulsar/Classy"

                developers {
                    developer {
                        id = "gdpulsar"
                        name = "Pulsar"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/GDPulsar/Classy.git"
                    developerConnection = "scm:git:ssh://github.com:GDPulsar/Classy.git"
                    url = "https://github.com/GDPulsar/Classy"
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/GDPulsar/Librarby")
            credentials {
                username = findProperty("gpr.user")
                password = findProperty("gpr.token")
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}