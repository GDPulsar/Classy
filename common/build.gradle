plugins {
    id 'java-library'
    id 'maven-publish'
}

architectury {
    common rootProject.enabled_platforms.split(',')
}

loom {
    accessWidenerPath = file("src/main/resources/classy.classtweaker")
}

dependencies {
    // We depend on Fabric Loader here to use the Fabric @Environment annotations,
    // which get remapped to the correct annotations on each platform.
    // Do NOT use other classes from Fabric Loader.
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    modImplementation "dev.architectury:architectury:$rootProject.architectury_api_version"
}

publishing {
    publications {
        mavenCommon(MavenPublication) {
            from components.java

            artifactId = "classy-common"

            pom {
                name = "Classy! Common"
                description = "Common Architectury package for Classy!"
                url = "https://github.com/GDPulsar/Classy"

                developers {
                    developer {
                        id = "gdpulsar"
                        name = "Pulsar"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/GDPulsar/Classy.git"
                    developerConnection = "scm:git:ssh://github.com:GDPulsar/Classy.git"
                    url = "https://github.com/GDPulsar/Classy"
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/GDPulsar/Librarby")
            credentials {
                username = findProperty("gpr.user")
                password = findProperty("gpr.token")
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}
